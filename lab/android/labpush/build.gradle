apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion rootProject.ext.COMPILE_SDK_VERSION
    buildToolsVersion rootProject.ext.BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion rootProject.ext.MIN_SDK_VERSION
        targetSdkVersion rootProject.ext.TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
    }
    publishNonDefault true
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {

        }
    }
}
configurations.all{
    resolutionStrategy{
        force "com.android.support:appcompat-v7:${rootProject.ext.SUPPORT_VERSION}"
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:${rootProject.ext.SUPPORT_VERSION}"
    compile "com.facebook.react:react-native:+"
    debugCompile project(path: ":lablibrary", configuration: "debug")
    releaseCompile "com.lightappbuilder.lab4:lab-library:+"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.addStringOption("encoding", "UTF-8")
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    //archives javadocJar
    archives sourcesJar
}

if (android.publishNonDefault) {
    android.libraryVariants.all { variant ->
        if (variant.name == android.defaultPublishConfig) {
            def bundleTask = tasks["bundle${name.capitalize()}"]
            artifacts {
                archives(bundleTask.archivePath) {
                    classifier null
                    builtBy bundleTask
                }
            }
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri(rootProject.ext.repoOut))

            pom.project {
                name 'labpush'
                description "labpush"

                groupId 'com.lightappbuilder.lab4'
                artifactId 'labpush'
                version '1.0.0'
            }
        }
    }
}
